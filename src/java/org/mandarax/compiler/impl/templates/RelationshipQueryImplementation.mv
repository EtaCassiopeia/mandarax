package @{packageName};
@foreach{imp:context.importDeclarations} 
@{imp}
@end{}
import org.mandarax.rt.*;
import com.google.common.base.Predicate;

/**
 * Interface for queries for relationship <strong>@{rel.name}</strong>.
 * @version @{timestamp} 
 */
public class @{className} {

	// interface generated for queries
	
	@foreach{query:rel.getQueries()} 
	public ResultSet<@{rel.name}> @{query.name} ( @foreach{param:query.parameterNames} @{rel.getTypeNameForSlot(param)} @{param} @end{", "} ) {
		DerivationController _derivation = new DefaultDerivationController();
		return new ResultSet<@{rel.name}>(@{query.name} ( _derivation @if{query.hasParameters()},@end{} @foreach{param:query.parameterNames} @{param} @end{", "}),_derivation);
	} 
	@end{}
	
	// implementations - these methods are referenced by code generated from other rules in this package
	// and therefore kept static 
	@foreach{query:rel.getQueries()} 
	static ResourceIterator<@{rel.name}> @{query.name} ( final DerivationController _derivation @if{query.hasParameters()},@end{} @foreach{param:query.parameterNames} final @{rel.getTypeNameForSlot(param)} @{param} @end{", "} ) {
		final int _derivationlevel = _derivation.size();
		return new IteratorChain<@{rel.name}>(@{rel.rules.size()}) {
			
			public ResourceIterator<@{rel.name}> getNextIterator(int pos) {
			switch (pos) {
				@foreach{index:ruleIndices}
                		case @{index}: {
                			return @{query.name}_@{index}(_derivation.pop(_derivationlevel) @if{query.hasParameters()},@end{} @foreach{param:query.parameterNames} @{param} @end{", "});
                		}
				@end{}
				default: return EmptyIterator.DEFAULT;
			}}
		};
	} 
	@end{}
	
	
	// private methods - each method represents the invocation of a single rule for a certain query
	
	@foreach{query:rel.queries}// query: @{query.name}
	@foreach{index:ruleIndices}// rule: @{rel.rules.get(index)}
	private static ResourceIterator<@{rel.name}> @{query.name}_@{index} (final DerivationController _derivation @if{query.hasParameters()},@end{} @foreach{param:query.parameterNames} final @{rel.getTypeNameForSlot(param)} @{param} @end{", "}) {
		@code{rule=rel.getRules().get(index)}
		@if{rule.fact}@includeNamed{'FactInvocation'}
		@else{}@includeNamed{'RuleInvocation'}
		@end{}
	}
	@end{}@end{}
	
}

